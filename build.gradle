import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.github.hierynomus.license" version "0.13.1"
}

apply plugin: 'java'
apply plugin: 'maven'

repositories {
	jcenter()

	maven {
		url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
	}

	maven {
		url 'https://oss.sonatype.org/content/groups/public/'
	}

	maven {
		url 'http://maven.elmakers.com/repository/'
	}

	maven {
		url 'http://maven.sk89q.com/repo'
	}

	maven {
		url 'https://mvn.yawk.at'
	}

//	maven {
//		url 'http://nexus.theyeticave.net/content/repositories/pub_releases'
//	}

	maven {
		url 'https://repo.destroystokyo.com/repository/maven-public/'
	}
}

dependencies {
	compile 'com.destroystokyo.paper:paper-api:1.11.2-R0.1-SNAPSHOT'
	compile 'org.eclipse.jdt:org.eclipse.jdt.annotation:1.1.0'
	compile 'com.google.code.findbugs:findbugs:2.0.3'
	compile 'de.slikey:EffectLib:5.0'
	compile 'com.sk89q:worldguard:6.1.1-SNAPSHOT'
	compile 'net.milkbowl.vault:Vault:1.5.3'
	compile 'net.sacredlabyrinth.Phaed:PreciousStones:10.0.5'

	testCompile 'junit:junit:4.12'
	testCompile 'org.easymock:easymock:3.4'

	compile fileTree(dir: 'lib', include: '*.jar')
}

configurations.all {
	resolutionStrategy.dependencySubstitution {
		substitute module('org.bukkit:bukkit') with module('com.destroystokyo.paper:paper-api:1.11.2-R0.1-SNAPSHOT')
	}
}

processResources {
	filter ReplaceTokens, tokens: [
		"version" : project.property("version")
	]
}

jar {
	archiveName System.getenv('SKRIPT_JAR_NAME') == null ? 'Skript.jar' : System.getenv("SKRIPT_JAR_NAME")
}

license {
    header file('licenseheader.txt')
    exclude('*.sk')
}

configurations {
	ecj
}

dependencies {
	ecj 'org.eclipse.jdt.core.compiler:ecj:4.6.1'
}

compileJava {
	options.fork = true
	options.encoding = 'UTF-8'
	options.compilerArgs = ['-properties', '.settings/org.eclipse.jdt.core.prefs', '-encoding', 'UTF-8']
	options.forkOptions.with {
		executable = 'java'
		jvmArgs = ['-classpath', project.configurations.ecj.asPath, 'org.eclipse.jdt.internal.compiler.batch.Main']
	}
}

uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = "ch.njol"
        repository(url: "file:///itemapi_repo/")
    }
}
