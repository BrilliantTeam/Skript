test "furnace slot":
	# init
	set block at location(0, 0, 0, world) to furnace
	set {_furnace} to block at location(0, 0, 0, world)

	# ore slot test
	set ore slot of {_furnace} to 32 iron ores
	assert ore slot of {_furnace} is 32 iron ores with "expected ore slot to have '32 iron ores', but it had '%ore slot of {_furnace}%'"
	assert fuel slot of {_furnace} is air with "expected fuel slot to have nothing, but it had '%fuel slot of {_furnace}%'"
	assert result slot of {_furnace} is air with "expected result slot to have nothing, but it had '%result slot of {_furnace}%'"
	clear ore slot of {_furnace}
	assert ore slot of {_furnace} is air with "expected ore slot to have nothing, but it had '%ore slot of {_furnace}%'"

	# fuel slot test
	set fuel slot of {_furnace} to lava bucket
	assert fuel slot of {_furnace} is lava bucket with "expected fuel slot to have 'lava bucket', but it had '%fuel slot of {_furnace}%'"
	assert ore slot of {_furnace} is air with "expected ore slot to have nothing, but it had '%ore slot of {_furnace}%'"
	assert result slot of {_furnace} is air with "expected result slot to have nothing, but it had '%result slot of {_furnace}%'"
	clear fuel slot of {_furnace}
	assert fuel slot of {_furnace} is air with "expected fuel slot to have nothing, but it had '%fuel slot of {_furnace}%'"

	# ore slot test
	set result slot of {_furnace} to 32 iron ingots
	assert result slot of {_furnace} is 32 iron ingots with "expected result slot to have '32 iron ingots', but it had '%result slot of {_furnace}%'"
	assert ore slot of {_furnace} is air with "expected ore slot to have nothing, but it had '%ore slot of {_furnace}%'"
	assert fuel slot of {_furnace} is air with "expected fuel slot to have nothing, but it had '%fuel slot of {_furnace}%'"
	clear result slot of {_furnace}
	assert result slot of {_furnace} is air with "expected result slot to have nothing, but it had '%result slot of {_furnace}%'"

	# cleanup
	set block at location(0, 0, 0, world) to air
